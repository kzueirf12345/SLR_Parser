cmake_minimum_required(VERSION 3.21)
project(SLRParser LANGUAGES CXX)

set(CMAKE_EXPORT_COMPILE_COMMANDS ON)

set(CMAKE_CXX_STANDARD 20)
set(CMAKE_CXX_STANDARD_REQUIRED ON)
set(CMAKE_CXX_EXTENSIONS OFF) 

###===========================================FLAGS=================================================

option(SANITIZE "Enable Sanitizers" OFF)

if(MSVC)
    add_compile_options(/W4 /WX)
else()
    add_library(project_warnings INTERFACE)
    target_compile_options(project_warnings INTERFACE
        -Wall -Wextra -Werror -Wpedantic
        $<$<CONFIG:DEBUG>:-Og -g>
        $<$<CONFIG:RELEASE>:-O3 -DNDEBUG>
    )
endif()

if(SANITIZE AND NOT MSVC)
    add_library(project_sanitizers INTERFACE)
    target_compile_options(project_sanitizers INTERFACE
        -fsanitize=address,undefined
        -fno-omit-frame-pointer
        -fno-optimize-sibling-calls
    )
    target_link_options(project_sanitizers INTERFACE
        -fsanitize=address,undefined
        -fno-omit-frame-pointer
    )
endif()

###===========================================FLEX==================================================

find_package(FLEX REQUIRED)

###===========================================SUBDIRECTORY==========================================

add_subdirectory(source/utils)
add_subdirectory(source/args)
add_subdirectory(source/lexer)
add_subdirectory(source/syntaxer)

add_executable(SLRParser source/main.cpp)

###===========================================LIB_LINK==============================================

target_link_libraries(SLRParser PRIVATE project_warnings)

if(SANITIZE AND NOT MSVC)
    target_link_libraries(SLRParser PRIVATE project_sanitizers)
endif()

target_link_libraries(SLRParser PRIVATE
    utils
    args
    lexer
    syntaxer
)